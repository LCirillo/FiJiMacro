//This macro calculates the cortical level of a protein in C. elegans embryos
//it will create three folders and save independently the processed image,
//the line profile quantification, the cytoplasmic quantifications, and the
//ROI used during the processing.


// Get path to Save Data
  Quantification = getDirectory("Quantification");
  if (Quantification=="")
      exit("No temp directory available");
 roiManager("reset");
 run("Clear Results");
 //run("Select None");
 run("Close All");
  
//Open the image
 waitForUser("Open an Image");
 imageTitle = getTitle();

//Remove Extension
  x = getTitle();
  x = replace(x, ".tif", "");        
  x = replace(x, ".tiff", "");      
  x = replace(x, ".lif", "");      
  x = replace(x, ".lsm", "");    
  x = replace(x, ".czi", "");      
  x = replace(x, ".nd2", "");
  title = x;

//Preliminary settings 
 run("Set Measurements...", "area mean redirect=None decimal=3");
 roiManager("reset");
 run("Clear Results");
 run("Select None");
 selectWindow(imageTitle); 
 run("Grays");

// Create folders
 Results = Quantification + "Results" + File.separator;
 File.makeDirectory(Results);
 if (!File.exists(Results))
     exit("Unable to create directory");
 print("");
 print(Results);

 ROI = Quantification + "ROI" + File.separator;
 File.makeDirectory(ROI);
 if (!File.exists(ROI))
     exit("Unable to create directory");
 print("");
 print(ROI);

 Projections =  Quantification + "Projections" + File.separator;
 File.makeDirectory(Projections);
 if (!File.exists(Projections))
     exit("Unable to create directory");
 print("");
 print(Projections);

//image processing - SubtractBackground
run("Split Channels");
		selectWindow("C2-"+imageTitle);
		makeRectangle(600, 67, 183, 183);
		run("Set Measurements...", "mean redirect=None decimal=3");
 		waitForUser("SelectBackground");
 		roiManager("Add");
		run("Measure");
		BG=getResult("Mean");
		run("Subtract...", "value=BG stack");
		run("Clear Results");
		roiManager("Select", 0);
        roiManager("Rename", "BackGround");
        run("Select None");
		selectWindow("C2-"+imageTitle);
		run("Z Project...", "projection=[Max Intensity]");		
saveAs("Tiff", Projections + "MAX_" + title);
MAX = getTitle();

//image processing - Cytosol AB and P1
selectWindow(MAX);
	makeRectangle(600, 67, 90, 90);
	waitForUser("Select AB");
 	roiManager("Add");
 	roiManager("Select", 1);
 	roiManager("Rename", "AB_CYTO");
	run("Measure");
  	saveAs("Results", Results + "MAX_AB_Cyto " + title + ".csv");
 	run("Clear Results");
	makeRectangle(600, 67, 90, 90);
	waitForUser("Select P1");
 	roiManager("Add");
 	roiManager("Select", 2);
 	roiManager("Rename", "P1_CYTO");
	run("Measure");
  	saveAs("Results", Results + "MAX_P1_Cyto " + title + ".csv");
 	run("Clear Results");
 	
//Line Profile (MAX)
selectWindow(MAX);
setTool("polyline");
run("Plots...", "width=530 height=300 font=12 minimum=0 maximum=0");
waitForUser("Select_AB_Cortex");
  roiManager("Add");
  roiManager("Select", 3);
  roiManager("Rename", "AB");
  run("Clear Results");
  run("Plot Profile");
  Plot.getValues(x, y);
    lt = x.length;
    for (j=0; j<lt; j++)
             {setResult("Plot Values", j, y[j]);
              setResult("Distance", j, x[j]);}
    updateResults;

  saveAs("Results", Results + "MAX_AB " + title + ".csv");
  run("Clear Results");
  close();
selectWindow(MAX);
waitForUser("Select_P1_Cortex");
  roiManager("Add");
  roiManager("Select", 4);
  roiManager("Rename", "P1");
  run("Clear Results");
  run("Plot Profile");
  Plot.getValues(x, y);
    lt = x.length;
    for (j=0; j<lt; j++)
             {setResult("Plot Values", j, y[j]);
              setResult("Distance", j, x[j]);}
    updateResults;
  //Plot.getValues(x, y);
  saveAs("Results", Results + "MAX_P1 " + title + ".csv");
  close();
  roiManager("Save", ROI + "Profiles " + title + ".zip");


 run("Close All");
 roiManager("reset");
 run("Clear Results");
